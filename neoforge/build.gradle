plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

repositories {
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "Shedaniel's Maven"
        url = "https://maven.shedaniel.me/"
    }
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    def resources = project(':common').file('src/main/resources/')
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            if(it.name == "data") {
                ideName = "Data Generation (${project.path})" // Unify the run config names with fabric
            }else{
                ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
            }
        }
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/main/generated/').getAbsolutePath(), '--existing', resources.getAbsolutePath()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    compileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")
    runtimeOnly("mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}")

    compileOnly("me.shedaniel:RoughlyEnoughItems-api-neoforge:$rei_version")
    runtimeOnly("me.shedaniel:RoughlyEnoughItems-neoforge:$rei_version")

    jarJar(implementation "org.commonmark:commonmark") {
        version {
            strictly '[0.1, 1.0)'
            prefer '0.24.0'
        }
    }
    additionalRuntimeClasspath("org.commonmark:commonmark:0.24.0")
}

sourceSets.main.resources { srcDir 'src/main/generated' }